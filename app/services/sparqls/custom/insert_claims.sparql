PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX adr: <http://kg.artsdata.ca/resource/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX schema: <http://schema.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
insert {
    GRAPH ?mint_graph {
        << ?mint_entity ?props ?obj >> 
            prov:wasDerivedFrom [ wd:P854 ?source_entity  ;
                                  wd:P248 ?source_dataset   ] .
    }
}
where {
    values ?props {
        <http://www.wikidata.org/prop/direct/P8860> schema:url schema:legalName 
    }
    values ?source_graph {
        <http://kg.artsdata.ca/culture-creates/huginn/capacoa-members>
    }
    ?source_graph prov:wasDerivedFrom/foaf:primaryTopic ?source_dataset .
    bind (concat("http://kg.artsdata.ca/minted/",strbefore(strafter(str(?mint_entity),"resource/"),"-"))  as ?mint_graph )
    {
        select ?source_entity ?mint_entity {
            graph ?source_graph {
                ?source_entity a schema:Organization ;
                               schema:sameAs ?mint_entity .
            }
        }
    }
    graph ?source_graph {
        ?source_entity ?props ?obj .
        filter(!isblank(?obj))
        filter not exists {
            ?shacl sh:focusNode ?source_entity ;
                   sh:resultPath ?props .
        }
    }
}