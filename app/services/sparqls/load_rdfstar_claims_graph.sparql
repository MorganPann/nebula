# load_rdfstar_claims_graph

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX onto: <http://www.ontotext.com/>
PREFIX schema: <http://schema.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {
    ?s ?p ?o  .

    <<?s ?p ?o>> ?a ?b .
    ?p rdfs:label ?prop_label .
    ?o rdfs:label ?obj_label .
    ?a rdfs:label ?a_label .
    ?b rdfs:label ?b_label .

    <<?s ?p ?o>> ?a ?qbns .
    ?qbns ?qbnp ?qbno .
    ?qbnp rdfs:label ?qbnp_label .
    ?qbno rdfs:label ?qbno_label .

    ?s ?p ?bns .
    ?bns ?bnp ?bno .
    ?bnp rdfs:label ?bnp_label .
    ?bno rdfs:label ?bno_label .

    ?bns ?bnp ?2bns .
    ?2bns ?2bnp ?2bno .
    ?2bnp rdfs:label ?2bnp_label .  
    ?2bno rdfs:label ?2bno_label .


    ?shacl_bn sh:focusNode ?s ; 
        ?shacl_pred ?shal_obj .

    ?shacl_bn2 sh:focusNode ?bns ; 
        ?shacl_bn2_pred ?shal_bn2_obj .
    
}
FROM onto:explicit 
WHERE {
    values ?s {
        <entity_uri_placeholder> 
    }

    # main triple labels
    OPTIONAL {
        ?o rdfs:label ?obj_label .
    }
    OPTIONAL {
        ?p rdfs:label ?prop_label .
    }
    OPTIONAL {
        ?o schema:name ?obj_name .
    }

    # quoted triple
    <<?s ?p ?o>> ?a ?b .

        FILTER NOT EXISTS {?s ?p ?anything }
     
  
    # quoted triple labels
    OPTIONAL {
        <<?s ?p ?o>> ?a ?b .
        ?a rdfs:label ?a_label .
    }
    OPTIONAL {
        <<?s ?p ?o>> ?a ?b .
        ?b rdfs:label ?b_label .
    }
    # quoted triple blank nodes
    OPTIONAL {
        <<?s ?p ?o>> ?a ?qbns .
        filter(isBLANK(?qbns))

        ?qbns ?qbnp ?qbno .
       
        OPTIONAL {
            ?qbnp rdfs:label ?qbnp_label .
        }
        OPTIONAL {
            ?qbno rdfs:label ?qbno_label .
        }
        
    }

    # blank nodes
     OPTIONAL {
        ?s ?p ?bns .
        filter(isBLANK(?bns))

        OPTIONAL {
            ?shacl_bn2 sh:focusNode ?bns ; 
                ?shacl_bn2_pred ?shal_bn2_obj .
        }

        ?bns ?bnp ?bno .
        OPTIONAL {
            ?bnp rdfs:label ?bnp_label .
        }
        OPTIONAL {
            ?bno rdfs:label ?bno_label .
        }

        # second level blank nodes
        OPTIONAL {
            ?bns ?bnp ?2bns .
            filter(isBLANK(?2bns))

            ?2bns ?2bnp ?2bno .
            OPTIONAL {
                ?2bnp rdfs:label ?2bnp_label .
            }
            OPTIONAL {
                ?2bno rdfs:label ?2bno_label .
            }

       }
     }

   
} order by   ?prop_label ?obj_label ?a 
