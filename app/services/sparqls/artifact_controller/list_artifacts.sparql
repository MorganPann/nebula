PREFIX schema: <http://schema.org/>
PREFIX ado: <http://kg.artsdata.ca/ontology/>

SELECT 
?account 
?group 
?artifact 
?artifact_uri
?artsdata_graph
(max(?version) as ?latest_version) 
(count(?dataset) as ?versions) 
?auto_load
(sample(?auto_mints) as ?auto_mint)
(GROUP_CONCAT(distinct ?publisher ;
        separator=", ") as ?publishers) 
?action
WHERE {
    values ?account {
        "DATABUS_ACCOUNT" 
    }
    ?artifact_uri a <http://dataid.dbpedia.org/ns/core#Artifact> .
    filter(STRSTARTS(str(?artifact_uri), CONCAT("http://kg.artsdata.ca/databus/",str(?account))))
    OPTIONAL {
        ?dataset <http://dataid.dbpedia.org/ns/core#account> ?account ;
                 <http://dataid.dbpedia.org/ns/core#artifact> ?artifact_uri ;
                 <http://purl.org/dc/terms/publisher> ?publisher ;
                 <http://purl.org/dc/terms/hasVersion> ?version .
        OPTIONAL {
            ?artifact_uri ado:automint ?auto_mints .
        }
    }
    
    bind(STRAFTER(str(?artifact_uri),CONCAT(?account,"/")) as ?last_part)
    bind(concat("<a href='",str(?artifact_uri),"'>",STRAFTER(str(?last_part),"/"),"</a>") as ?artifact)
    bind(STRBEFORE(str(?last_part),"/") as ?group)
    bind(concat("<div class='d-flex'><a class='btn btn-primary btn-sm disabled me-1' href='#'>create</a> <a class='btn btn-danger btn-sm disabled' href='#'>delete</a></div>") as ?action)
    bind("true" as ?auto_load)
} group by  ?artifact_uri  ?artsdata_graph ?account  ?group ?auto_load 
?artifact ?action limit 100