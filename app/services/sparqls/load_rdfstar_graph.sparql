PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX onto: <http://www.ontotext.com/>
PREFIX schema: <http://schema.org/>

CONSTRUCT {
    ?s ?p ?o  .
    <<?s ?p ?o>> ?a ?b .
    ?p rdfs:label ?prop_label .
    ?o rdfs:label ?obj_label .
    ?a rdfs:label ?a_label .
    ?b rdfs:label ?b_label .

    ?s ?p ?bns .
    ?bns ?bnp ?bno .
    ?bnp rdfs:label ?bnp_label .
    ?bno rdfs:label ?bno_label .

}
FROM onto:explicit 
WHERE {
    values ?s {
        <entity_uri_placeholder> 
    }

    ?s ?p ?o  .

    # main triple labels
    OPTIONAL {
        ?o rdfs:label ?obj_label .
    }
    OPTIONAL {
        ?p rdfs:label ?prop_label .
    }
    OPTIONAL {
        ?o schema:name ?obj_name .
    }

    # quoted triple labels
    OPTIONAL {
        <<?s ?p ?o>> ?a ?b .
        ?a rdfs:label ?a_label .
    }
    OPTIONAL {
        <<?s ?p ?o>> ?a ?b .
        ?b rdfs:label ?b_label .
    }

    # blank nodes
     OPTIONAL {
       ?s ?p ?bns .
       filter(isBLANK(?bns))

       ?bns ?bnp ?bno .
       OPTIONAL {
        ?bnp rdfs:label ?bnp_label .
       }
       OPTIONAL {
        ?bno rdfs:label ?bno_label .
       }
     }

   
} order by   ?prop_label ?obj_label ?a 
