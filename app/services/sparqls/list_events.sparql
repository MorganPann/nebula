PREFIX schema: <http://schema.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
SELECT ?uri (sample(?name_language) as ?name)  (sample(?location_name) as ?location)    ?startDate ?adid ?resultSeverity
WHERE {
    graph <PLACEHOLDER> {
        ?uri schema:name  ?name_language ;
             a schema:Event ;
             schema:startDate ?startDate . 
       
        OPTIONAL {
            ?shacl sh:focusNode ?uri ;
                sh:resultSeverity ?resultSeverity .
        }
    } 
    OPTIONAL {
      ?adid_sub schema:sameAs ?uri .
      filter(contains(str(?adid_sub),"http://kg.artsdata.ca/resource/K"))
    }

    OPTIONAL {
      ?uri schema:sameAs ?adid_obj .
      filter(contains(str(?adid_obj),"http://kg.artsdata.ca/resource/K"))
    }

    bind(COALESCE(?adid_obj, ?adid_sub) as ?adid)

    OPTIONAL {
        ?uri  schema:location/schema:name ?location_name .
    }
    

} GROUP BY ?uri  ?startDate   ?resultSeverity ?adid
ORDER BY ?startDate